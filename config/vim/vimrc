" +-------+
" | vimrc |
" +-------+

set nocompatible
filetype off

" Runtimepath initialization

let s:bundle_ignore =
  \ [ 'vim-sexp'
  \ , 'vim-sexp-mappings-for-regular-people'
  \ , 'cscope_macros.vim'
  \ ]

let s:plugin_path = split($VIM_PLUGIN_PATH, ':')

for plugin in split(system('ls $VIM_BUNDLE'), '\n', 0)
  if index(s:bundle_ignore, plugin) < 0
    call add(s:plugin_path, $VIM_BUNDLE.'/'.plugin)
  endif
endfor

let s:plugin_after_path = []
for dir in s:plugin_path
  call add(s:plugin_after_path, dir.'/after')
endfor

" This order is based on :h initialization
let s:rtp = [ $VIM.'/pre' ]
        \ + s:plugin_path
        \ + [ $VIMRUNTIME ]
        \ + s:plugin_after_path
        \ + [ $VIM.'/post' ]

let &rtp = join(s:rtp, ',')

" Source actual configuration

for f in split(system("find $VIM/lib -name '*.vim' | sort"))
  exec 'source' f
endfor

for f in split(system("find $VIM/init -name '*.vim' | sort"))
  exec 'source' f
endfor

" Allow for project-specific options/mappings

set exrc
set secure

filetype plugin indent on
