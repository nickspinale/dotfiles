" +-------+
" | vimrc |
" +-------+

set nocompatible
filetype off

let g:my_vim_dir = fnamemodify(expand('$VIM'), ':h:h')

function MyVimFile(path)
  return g:my_vim_dir.'/'.a:path
endfunction

let s:my_plugin_list = '/run/current-system/sw/share/vim-plugins/annoying.rtp'

" Runtimepath initialization

let s:bundle_plugins =
  \ [ 'vim-surround'
  \ , 'vim-commentary'
  \ , 'vim-dirvish'
  \ , 'vim-repeat'
  \ , 'vim-bufferline'
  \ , 'vim-colors-solarized'
  \ , 'ultisnips'
  \ , 'vim-snippets'
  \ , 'vim-nix'
  \ ]
  "  \ , 'tabular'
  "  \ , 'vim-sexp'
  "  \ , 'vim-sexp-mappings-for-regular-people'
  "  \ , 'vim-slime'
  "  \ , 'cscope_macros.vim'

let s:plugins = []

for plugin in s:bundle_plugins
  call add(s:plugins, '/home/nick/vim-bundle/bundle/'.plugin)
endfor

if system('uname -a') =~ 'NixOS'
  let s:plugins += readfile(s:my_plugin_list)
endif

let s:plugins_after = []
for dir in s:plugins
  call add(s:plugins_after, dir.'/after')
endfor

" This order is based on :h initialization
let s:rtp = [ MyVimFile('pre') ]
        \ + s:plugins
        \ + [ expand('$VIMRUNTIME') ]
        \ + s:plugins_after
        \ + [ MyVimFile('post') ]

let &rtp = join(s:rtp, ',')

" Source actual configuration

for f in split(glob(g:my_vim_dir . 'vimrc.d/lib/*'))
  exec 'source' f
endfor

for modules in split(glob(MyVimFile('vimrc.d/modules.*')))
  for module in split(glob(modules.'/*.vim'))
    exec 'source' module
  endfor
endfor

" Allow for project-specific options/mappings

if filereadable('local.vim')
  source local.vim
endif

filetype plugin indent on

