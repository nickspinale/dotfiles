" +-------+
" | vimrc |
" +-------+

set nocompatible
filetype off

let g:vim_config_dir = fnamemodify(expand('$VIM'), ':h:h').'/'

if system('uname -a') =~ 'NixOS'
  let s:plugin_lists = [ '/run/current-system/sw/share/vim-plugins/lame.rtp'
                     \ , '/run/current-system/sw/share/vim-plugins/annoying.rtp'
                     \ ]
else
  let s:plugin_lists = ['~/.vimrc.rtp']
endif

let s:rtp_list = [expand('$VIMRUNTIME'), g:vim_config_dir.'vim.1']

for plugin_list in s:plugin_lists
  for plugin in readfile(plugin_list)
    call add(s:rtp_list, plugin)
    call add(s:rtp_list, plugin.'/after')
  endfor
endfor

let &rtp = join(s:rtp_list, ',')

for f in split(glob(g:vim_config_dir . 'vimrc.d/lib/*'))
  exec 'source '.f
endfor

for modules in split(glob(g:vim_config_dir.'vimrc.d/modules.*'))
  for module in split(glob(modules.'/*.vim'))
    exec 'source '.module
  endfor
endfor

if filereadable('local.vim')
  source local.vim
endif

filetype plugin indent on

